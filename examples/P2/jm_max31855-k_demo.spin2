'' =================================================================================================
''
''   File....... jm_max31855-k_demo.spin2
''   Purpose.... Demonstration program for MAX31855 thermocouple interface
''   Author..... Jon "JonnyMac" McPhalen
''               Copyright (c) 2020 Jon McPhalen
''               -- see below for terms of use
''   E-mail..... jon.mcphalen@gmail.com
''   Started....
''   Updated.... 07 SEP 2020
''
''   {$P2}
''
'' =================================================================================================


'' Tested with:
'' -- https://www.sparkfun.com/products/13266


con { timing }

  CLK_FREQ = 200_000_000                                        ' system freq as a constant
  MS_001   = CLK_FREQ / 1_000                                   ' ticks in 1ms
  US_001   = CLK_FREQ / 1_000_000                               ' ticks in 1us

  BR_TERM  = 115_200                                            ' terminal baud rate

  _clkfreq = CLK_FREQ                                           ' set system clock


con { fixed io pins }

  RX1      = 63  { I }                                          ' programming / debug
  TX1      = 62  { O }

  SF_CS    = 61  { O }                                          ' serial flash
  SF_SCK   = 60  { O }
  SF_SDO   = 59  { O }
  SF_SDI   = 58  { I }

  SD_SCK   = 61  { O }                                          ' sd card
  SD_CS    = 60  { O }
  SD_SDO   = 59  { O }
  SD_SDI   = 58  { I }

  SDA1     = 57  { IO }                                         ' i2c (optional)
  SCL1     = 56  { IO }


con { app io pins }

  TC_CS    = 26  { O }                                          ' for thermocouple interface
  TC_CLK   = 25  { O }
  TC_SDI   = 24  { I }


con

  #true,  ON, OFF
  #false, NO, YES


obj

' main                                                          ' * master Spin cog
  tmp  : "jm_max31855"                                          '   max31855 interface
  term : "jm_fullduplexserial"                                  ' * serial IO for terminal

' * uses cog when loaded


pub main() | faults, tc, tf, cj, dirty

  setup()

  wait_for_terminal(true)

  term.str(string("P2 Thermocouple Demo", 13))
  term.str(string("--------------------", 13))

  repeat
    term.fstr0(string("\002\000\002"))                          ' position cursor

    faults := tmp.read_faults(true)                             ' resample, get faults
    if (faults == %000)
      if (dirty)
        term.tx(term.CLR_DN)                                    ' remove last faults report
      tc := tmp.read_tc(false) * 25                             ' convert to 0.01 units
      tf := tc * 9 / 5 + 32_00                                  ' C to F
      cj := tmp.read_cj(false) * 625 / 100                      ' convert to 0.01 units
      term.fstr1(string("TC degrees: %6.2f\176C\r"), tc)
      term.fstr1(string("            %6.2f\176F\r"), tf)
      term.fstr1(string("CJ degrees: %6.2f\176C\r"), cj)
      dirty := false

    else
      term.tx(term.CLR_DN)                                      ' remove last temp display
      term.fstr0(string("Fault(s):\r"))
      if (faults & %001)
        term.fstr1(string("-- %s\r"), tmp.fault_str(%001))
      if (faults & %010)
        term.fstr1(string("-- %s\r"), tmp.fault_str(%010))
      if (faults & %100)
        term.fstr1(string("-- %s\r"), tmp.fault_str(%100))
      dirty := true

    waitms(1000)


pub setup()

  tmp.start(TC_SDI, TC_CLK, TC_CS)                              ' start tc interface

  term.start(RX1, TX1, %0000, BR_TERM)                          ' start terminal io


pub wait_for_terminal(clear)

  ' Download to RAM with F10
  ' F12 to open PST
  ' Click [Enable]
  ' Press Enter

  term.rxflush()
  term.rx()
  if (clear)
    term.tx(term.CLS)


con { license }

{{

  Terms of Use: MIT License

  Permission is hereby granted, free of charge, to any person obtaining a copy of this
  software and associated documentation files (the "Software"), to deal in the Software
  without restriction, including without limitation the rights to use, copy, modify,
  merge, publish, distribute, sublicense, and/or sell copies of the Software, and to
  permit persons to whom the Software is furnished to do so, subject to the following
  conditions:

  The above copyright notice and this permission notice shall be included in all copies
  or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
  INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
  PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
  CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE
  OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

}}